use dataverse tpch;

from $c in dataset('Customer')
from $o in dataset('Orders')
where $c.c_custkey = $o.o_custkey
from $t in (
  from $l in dataset('LineItem')
  group by $l_orderkey := $l.l_orderkey keeping $l
  select {
    "l_orderkey": $l_orderkey,
    "t_sum_quantity": sum(from $i in $l select $i.l_quantity)
  }
)
where $o.o_orderkey = $t.l_orderkey and $t.t_sum_quantity > 30
from $l in dataset('LineItem')
where $l.l_orderkey = $o.o_orderkey
group by $c_name := $c.c_name, $c_custkey := $c.c_custkey, $o_orderkey := $o.o_orderkey,
         $o_orderdate := $o.o_orderdate, $o_totalprice := $o.o_totalprice keeping $l
order by $o_totalprice desc, $o_orderdate
limit 100
select {
  "c_name": $c_name,
  "c_custkey": $c_custkey,
  "o_orderkey": $o_orderkey,
  "o_orderdate": $o_orderdate,
  "o_totalprice": $o_totalprice,
  "sum_quantity": sum(from $j in $l select $j.l_quantity)
}

